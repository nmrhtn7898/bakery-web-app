plugins {
    id "org.asciidoctor.convert" version "1.5.9.2"
}

ext {
    snippetsDir = file("${buildDir}/generated-snippets")
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    dependsOn(test)
    inputs.dir snippetsDir
}

bootJar {
    dependsOn(asciidoctor)
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

task copyFile {
    doLast {
        copy {
            from 'src/docker/run.sh'
            into "${buildDir}/libs"
        }
        copy {
            from 'src/docker/Dockerfile'
            into "${buildDir}/libs"
        }
    }
}

tasks.build.finalizedBy(copyFile)

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation ('it.ozimov:embedded-redis:0.7.3') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}
